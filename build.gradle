plugins {
    id 'groovy'
}

group 'org.example'
version '1.0-SNAPSHOT'

configurations {
    targetDxpBom
    targetDxpCompileBom
}

project.ext.onlyApis = false

// From: https://repository.liferay.com/nexus/service/local/repositories/liferay-public-releases/content/com/liferay/portal/release.dxp.bom/maven-metadata.xml
//def targetVersion = "7.2.10.8";
//def targetVersion = "7.3.10.u17";
//def targetVersion = "7.3.10.3";
//def targetVersion = "7.3.10.u35";
//def targetVersion = "7.4.13.u112";
def targetVersion = "2024.q1.5";

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.3.11'

    targetDxpBom "com.liferay.portal:release.dxp.bom:${targetVersion}@pom"
    targetDxpCompileBom "com.liferay.portal:release.dxp.bom.compile.only:${targetVersion}@pom"

    def addDep = { Map conf -> implementation group: "${conf.g}", name: "${conf.n}", version: "${conf.v}" }
    def dxpBom = configurations.targetDxpBom.singleFile
    def dxpCompileBom = configurations.targetDxpCompileBom.singleFile
    parseDepManagement(dxpBom, addDep)
    parseDepManagement(dxpCompileBom, addDep)
}

def parseDepManagement(File bomFile, lambda) {
    def xml = new XmlSlurper().parse(bomFile)

    xml.dependencyManagement.dependencies.dependency.each {
        if (!project.onlyApis || (it.artifactId as String).contains("kernel") || (it.artifactId as String).contains("api"))
            lambda g: it.groupId, n: it.artifactId, v: it.version
    }
}
